# $Id$
# Maintainer: MediaArea.net SARL <Info@MediaArea.net>

pkgname=('dvrescue' 'dvrescue-gui')
pkgver=24.07
pkgrel=1
pkgdesc="Supplies technical and tag information about a video or audio file"
url="http://MediaArea.net"
license=('BSD-3-Clause')
source=(${pkgname}_${pkgver}.orig.tar.xz)
md5sums=('00000000000000000000000000000000')
arch=('i686' 'x86_64')
makedepends=('libzen>=0.4.41' 'libmediainfo>=24.06' 'qt5-base' 'qt5-quickcontrols2' 'qt5-svg' 'qt5-xmlpatterns' 'qwt' 'qt5-tools' 'yasm' 'alsa-lib')

prepare() {
    cd "${srcdir}"/dvrescue/dvrescue/Project/GNU/CLI
    sh ./autogen
    ./configure --prefix=/usr

    cd "${srcdir}"/dvrescue/ffmpeg
    ./configure --enable-gpl --disable-autodetect --enable-alsa --disable-doc --disable-programs --disable-debug --enable-pic --enable-static --enable-lto --disable-shared --prefix=`pwd`

    mkdir "${srcdir}"/dvrescue/dvrescue/Source/GUI/dvrescue/build
    cd "${srcdir}"/dvrescue/dvrescue/Source/GUI/dvrescue/build
    USE_SYSTEM=true qmake-qt5 -recursive BINDIR=/usr/bin
}

build() {
    cd "${srcdir}"/dvrescue/dvrescue/Project/GNU/CLI
    make

    cd "${srcdir}"/dvrescue/ffmpeg
    make

    cd "${srcdir}"/dvrescue/dvrescue/Source/GUI/dvrescue/build
    make
}

package_dvrescue() {
    pkgdesc="${pkgdesc} (CLI)"
    depends=('libzen>=0.4.41' 'libmediainfo>=24.06' 'xmlstarlet')

    cd "${srcdir}"/dvrescue/dvrescue/Project/GNU/CLI
    make DESTDIR="${pkgdir}" install

    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/History.txt "${pkgdir}"/usr/share/doc/${pkgname}/History.txt
}

package_dvrescue-gui() {
    pkgdesc="${pkgdesc} (GUI)"
    depends=('qt5-base' 'qt5-quickcontrols2' 'qt5-svg' 'qt5-xmlpatterns' 'qwt' 'alsa-lib')

    install -D -m 0755 "${srcdir}"/dvrescue/dvrescue/Source/GUI/dvrescue/build/dvrescue/dvrescue "${pkgdir}"/usr/bin/dvrescue-gui
    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/History.txt "${pkgdir}"/usr/share/doc/${pkgname}/History.txt

    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/Source/GUI/dvrescue/dvrescue/icons/icon.png "${pkgdir}"/usr/share/pixmaps/dvrescue.png
    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/Project/GNU/GUI/dvrescue-gui.desktop "${pkgdir}"/usr/share/applications/dvrescue-gui.desktop
    install -D -m 0644 "${srcdir}"/dvrescue/dvrescue/Project/GNU/GUI/dvrescue-gui.metainfo.xml "${pkgdir}"/usr/share/metainfo/dvrescue-gui.metainfo.xml
}
